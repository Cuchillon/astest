plugins {
    id 'io.qameta.allure' version '2.8.1'
    id 'java'
}

group 'com.ferick'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

allure {
    autoconfigure = true
    aspectjweaver = true
    version = '2.13.6'
    clean = true
    useJUnit5 {
        version = '2.13.6'
    }
}

dependencies {
    implementation 'org.apache.commons:commons-configuration2:2.7'
    implementation 'javax.inject:javax.inject:1'
    implementation 'io.rest-assured:rest-assured:4.4.0'
    implementation 'io.rest-assured:json-schema-validator:4.4.0'
    implementation 'io.rest-assured:json-path:4.4.0'
    implementation 'io.qameta.allure:allure-junit5:2.13.6'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.yaml:snakeyaml:1.26'
    implementation 'com.github.Cuchillon:datagen:e0a5896b25542234800a988e29e2534ae56791ac'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    runtime 'org.aspectj:aspectjweaver:1.9.6'
}

test {
    def props = ['common.baseUrl']
    jvmArgs += loadProperties(props)
    def tagString = project.hasProperty('tags') ? project.tags : ''
    String[] tags = !tagString.isEmpty() ? tagString.split(',') : []
    useJUnitPlatform {
        if (tags.size() > 0) {
            includeTags(tags)
        }
    }
}

def loadProperties(List<String> properties) {
    def args = []
    properties.forEach({
        def value = findProperty(it) ?: ''
        if (value != '') {
            args.add("-D${it}=${value}")
        }
    })
    return args
}